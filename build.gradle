apply plugin: 'groovy'

sourceSets {
  jobs {
    groovy {
      srcDirs 'src/jobs'
      compileClasspath += main.compileClasspath
    }
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

repositories {
  mavenCentral()
  maven { url 'https://repo.jenkins-ci.org/releases/' }
  jcenter()
}

configurations {
  testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
  implementation 'org.codehaus.groovy:groovy-all:2.4.11'
  implementation "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

  // for the rest task
  implementation('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
    exclude(module: 'groovy')
  }
  implementation('org.apache.ant:ant:1.9.7')

  testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4'
  testImplementation 'cglib:cglib-nodep:2.2.2' // used by Spock

  // Jenkins test harness dependencies
  testImplementation('org.jenkins-ci.main:jenkins-test-harness:2.33') {
    exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'
    // https://github.com/sheehan/job-dsl-gradle-example/issues/90
  }
  testImplementation("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}") {
    exclude group: 'org.jenkins-ci.ui', module: 'bootstrap'
    // https://github.com/sheehan/job-dsl-gradle-example/issues/87
  }

  // Job DSL plugin including plugin dependencies
  testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
  testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
  testImplementation 'org.jenkins-ci.plugins:structs:1.20@jar'
  testImplementation 'org.jenkins-ci.plugins:cloudbees-folder:6.9@jar'
  testImplementation 'org.jenkins-ci.plugins:script-security:1.68@jar'

  // Run the following script in the Script Console of your Jenkins instance to generate
  // the above testPlugins list. (adapted from https://git.io/fjpUs)
  //  note that I did not install all of these, I merely run './gradlew --info test' and only install the ones that
  //  are used for the tests or throw a traceback.
  /*
    Jenkins.instance.pluginManager.plugins
        .findAll { !(it.shortName in ['job-dsl', 'structs']) }
        .collect { "testPlugins '${it.manifest.mainAttributes.getValue("Group-Id")}:${it.shortName}:${it.version}'" }
        .sort()
        .each { println it }
 */

  // plugins to install in test instance
  // These are the Maven IDs for the given plugins, which can be found at:
  // https://<jenkins.hostname>/pluginManager/plugin/<plugin ID>/thirdPartyLicenses
  // http://ci-ops-jenkins-update-site.rhev-ci-vms.eng.rdu2.redhat.com/update-center/2.235/default/plugins.txt
  testPlugins 'org.jenkins-ci.plugins:ansible:1.1'
  testPlugins 'org.jenkins-ci.plugins:ansible-tower:0.16.0'
  testPlugins 'com.coravy.hudson.plugins.github:github:1.32.0'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.17'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.87'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.23'
  testPlugins 'org.jenkins-ci.plugins:any-buildstep:0.1'
  testPlugins 'org.jenkins-ci.plugins:build-blocker-plugin:1.7.3'
  testPlugins 'org.jenkins-ci.plugins:cachet-gating:1.0.7'
  testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.15'
  testPlugins 'org.jenkins-ci.plugins:credentials:2.3.14'
  testPlugins 'org.jenkins-ci.plugins:cvs:2.16'
  testPlugins 'org.jenkins-ci.plugins:envinject:2.3.0'
  testPlugins 'org.jenkins-ci.plugins:flexible-publish:0.15.2'
  testPlugins 'org.jenkins-ci.plugins:generic-webhook-trigger:1.67'
  testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.2'
  testPlugins 'org.jenkins-ci.plugins:git:4.5.2'
  testPlugins 'org.jenkins-ci.plugins:gradle:1.36'
  testPlugins 'org.jenkins-ci.plugins:groovy:2.3'
  testPlugins 'org.jenkins-ci.plugins:htmlpublisher:1.23'
  testPlugins 'org.jenkins-ci.plugins:jms-messaging:1.1.19'
  testPlugins 'org.jenkins-ci.plugins:junit:1.49'
  testPlugins 'org.jenkins-ci.plugins:mailer:1.32.1'
  testPlugins 'org.jenkins-ci.plugins:matrix-project:1.18'
  testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.37'
  testPlugins 'org.jenkins-ci.plugins:progress-bar-column-plugin:1.0'
  testPlugins 'org.jenkins-ci.plugins:ssh-agent:1.20'
  testPlugins 'org.jenkins-ci.plugins:token-macro:2.13'
  testPlugins 'org.jenkins-ci.plugins:view-job-filters:2.3'
  testPlugins 'org.jenkins-ci.plugins:ws-cleanup:0.38'
  testPlugins group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-aggregator', version: '2.6'
  testPlugins 'com.openshift.jenkins.plugins:openshift-client:1.0.34'
  testPlugins 'com.openshift.jenkins:openshift-pipeline:1.0.57'
  testPlugins 'io.fabric8.jenkins.plugins:openshift-sync:1.0.45'
  testPlugins 'org.openshift.jenkins:openshift-login:1.0.26'
  testPlugins 'org.jenkinsci.plugins:kubernetes-credentials:0.8.0'
  testPlugins 'com.cloudbees.jenkins.plugins:kubernetes-credentials-provider:0.15'
  testPlugins 'org.csanchez.jenkins.plugins:kubernetes:1.27.7'
  testPlugins 'org.jenkins-ci.plugins:kubernetes-client-api:4.13.2-1'
  testPlugins 'org.6wind.jenkins:lockable-resources:2.10'
  testPlugins 'org.jenkins-ci.plugins:dashboard-view:2.14'
  testPlugins 'org.jenkins-ci.plugins:extra-columns:1.22'
  testPlugins 'org.jenkins-ci.plugins:throttle-concurrents:2.1'
  testPlugins 'org.jenkins-ci.plugins:test-results-analyzer:0.3.5'
}

task resolveTestPlugins(type: Copy) {
  from configurations.testPlugins
  into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
  include '*.hpi'
  include '*.jpi'
  def mapping = [:]

  doFirst {
    configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
      mapping[it.file.name] = "${it.name}.${it.extension}"
    }
  }
  rename { mapping[it] }

  doLast {
    List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
    new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
  }
}

test {
  dependsOn tasks.resolveTestPlugins
  inputs.files sourceSets.jobs.groovy.srcDirs

  // set build directory for Jenkins test harness, JENKINS-26331
  systemProperty 'buildDirectory', project.buildDir.absolutePath
  //maxParallelForks = 2
  //maxHeapSize = '128m'
  //jvmArgs '-Xmx512m', '-Xms128m', "-XX:+UnlockExperimentalVMOptions", "-XX:+UseCGroupMemoryLimitForHeap"
}

task rest(dependsOn: 'classes', type: JavaExec) {
  main = 'com.dslexample.rest.RestApiScriptRunner'
  classpath = sourceSets.main.runtimeClasspath
  systemProperties System.getProperties()
}

